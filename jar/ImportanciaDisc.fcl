FUNCTION_BLOCK dificuldade

	VAR_INPUT				
		dificuldade : REAL;
		requisitada : REAL;
	END_VAR

	VAR_OUTPUT				
		recomendar : REAL;
	END_VAR

	FUZZIFY dificuldade
		TERM Alta  := gauss 0 25;
		TERM Media  := gauss 50 10;
		TERM Baixa  := gauss 100 25;		
	END_FUZZIFY

	FUZZIFY requisitada
		TERM Alta  := sigm 3 3;
		TERM Media  := gauss 2.5 1;
		TERM Baixa  := sigm -3 1;
	END_FUZZIFY


	DEFUZZIFY recomendar
		TERM MuitoAlta := (80,0)  (100,1);
		TERM Alta  := (50,0)  (75,1) (100,0);
		TERM Media  := (20,0)  (50,1) (80,0);
		TERM Baixa  := (0,0)   (25,1) (50,0);
		TERM MuitoBaixa := (0,1)   (20,0);
		
		// Use 'Center Of Gravity' defuzzification method
		METHOD : COG;
		// Default value is 0 (if no rule activates defuzzifier)
		DEFAULT := 0;
	END_DEFUZZIFY
	
	RULEBLOCK regras
		// Use 'min' for 'and' (also implicit use 'max'
		// for 'or' to fulfill DeMorgan's Law)
		AND : MIN;
		// Use 'min' activation method
		ACT : MIN;
		// Use 'max' accumulation method
		ACCU : MAX;
		
		RULE 1: IF requisitada IS Alta AND dificuldade IS Alta THEN recomendar IS MuitoAlta;
		RULE 2: IF requisitada IS Alta AND dificuldade IS Media THEN recomendar IS Alta;
		RULE 3: IF requisitada IS Alta AND dificuldade IS Baixa THEN recomendar IS Baixa;
		RULE 4: IF requisitada IS Media AND dificuldade IS Alta THEN recomendar IS Alta;
		RULE 5: IF requisitada IS Media AND dificuldade IS Media THEN recomendar IS Media;
		RULE 6: IF requisitada IS Media AND dificuldade IS Baixa THEN recomendar IS Baixa;			
		RULE 7: IF requisitada IS Baixa AND dificuldade IS Alta THEN recomendar IS Baixa;			
		RULE 8: IF requisitada IS Baixa AND dificuldade IS Media THEN recomendar IS Baixa;			
		RULE 9: IF requisitada IS Baixa AND dificuldade IS Baixa THEN recomendar IS MuitoBaixa;			
	
	END_RULEBLOCK	
	
END_FUNCTION_BLOCK
